import QtQuick
import QtQuick.Layouts
import Quickshell.Hyprland
import qs.Commons
import qs.Ui

BarWidget {
    id: root
    moduleName: "omarchy.workspaces"

    function workspaceById(id) {
        var values = Hyprland.workspaces.values;
        for (var i = 0; i < values.length; i++) {
            if (values[i].id === id)
                return values[i];
        }

        return null;
    }

    function workspaceIds() {
        var ids = [];
        var values = Hyprland.workspaces.values;
        var focusedId = Hyprland.focusedWorkspace !== null ? Hyprland.focusedWorkspace.id : -1;

        for (var i = 0; i < values.length; i++) {
            var id = values[i].id;
            var occupied = values[i].toplevels.values.length > 0;
            if (id > 0 && id <= 11 && (occupied || id === focusedId) && ids.indexOf(id) === -1)
                ids.push(id);
        }

        ids.sort(function (left, right) {
            return left - right;
        });
        return ids;
    }

    readonly property var workspaceIcons: ({
            "1": "0",
            "2": "",
            "3": "2",
            "4": "3",
            "5": "4",
            "6": "5",
            "7": "6",
            "8": "7",
            "9": "",
            "10": "",
            "11": ""
        })

    function workspaceLabel(id) {
        var icon = root.workspaceIcons[String(id)];
        return icon !== undefined ? icon : String(id);
    }

    function focusWorkspace(id) {
        if (!root.bar)
            return;
        root.bar.run("hyprctl dispatch " + Util.shellQuote("hl.dsp.focus({ workspace = \"" + id + "\" })"));
    }

    readonly property real trailingGap: root.vertical ? 0 : Style.spaceReal(1.5)

    implicitWidth: grid.implicitWidth + trailingGap
    implicitHeight: grid.implicitHeight

    GridLayout {
        id: grid
        anchors.fill: parent
        anchors.rightMargin: root.trailingGap
        columns: root.vertical ? 1 : root.workspaceIds().length
        columnSpacing: root.vertical ? 0 : Style.space(1)
        rowSpacing: root.vertical ? Style.space(2) : 0

        Repeater {
            model: root.workspaceIds()

            WidgetButton {
                required property int modelData

                readonly property var workspace: root.workspaceById(modelData)
                readonly property bool occupied: workspace !== null && workspace.toplevels.values.length > 0
                readonly property bool focused: Hyprland.focusedWorkspace !== null && Hyprland.focusedWorkspace.id === modelData

                bar: root.bar
                text: root.workspaceLabel(modelData)
                opacity: focused ? 1 : 0.5
                horizontalMargin: 6
                verticalPadding: 6
                fixedWidth: root.vertical ? root.barSize : Style.space(20)
                fixedHeight: root.barSize
                onPressed: function () {
                    root.focusWorkspace(modelData);
                }
            }
        }
    }
}
